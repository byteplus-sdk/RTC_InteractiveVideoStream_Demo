plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        ensureProperty('LOGIN_URL')
        buildConfigField('String', 'LOGIN_URL', "\"${project.LOGIN_URL}\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }
}
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    implementation "androidx.appcompat:appcompat:$AppCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$RecyclerViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$ConstraintLayoutVersion"
    implementation "com.google.android.material:material:$MaterialVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    api(name: 'BasicModule', ext: 'aar')
    api(name: 'BytePlusRTC', ext: 'aar')
    api(name: 'hfopenapi', ext: 'aar')

    implementation "org.greenrobot:eventbus:$EventBusVersion"
    implementation "com.google.code.gson:gson:$GsonVersion"

    implementation('io.socket:socket.io-client:1.0.1') {
        exclude group: 'org.json', module: 'json'
    }
}

private def ensureProperty(String propertyName) {
    if (!project.hasProperty(propertyName) || project[propertyName].isEmpty()) {
        throw new GradleException("NO ${propertyName} defined in gradle.properties!")
    }
}